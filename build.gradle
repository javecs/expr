buildscript {
    ext.kotlin_version = '1.2.20'
    ext.antlr_version = '4.7'
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group 'xyz.javecs.tools'
version '0.2.6'

apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:$antlr_version"
    compile "org.antlr:antlr4:$antlr_version"
    compile "org.antlr:antlr4-runtime:$antlr_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

generateGrammarSource {
    outputDirectory = file('src/main/java/xyz/javecs/tools/expr/parser')
    arguments = ['-no-listener', '-visitor', '-package', 'xyz.javecs.tools.expr.parser']
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "javecs"
            name "MUNI"
            email "moonhee.javecs@gmail.com"
        }
    }
}


publishing {
    publications {
        ExprPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'xyz.javecs.tools'
            artifactId 'expr'
            version = project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Javecs Tools Expr')
                root.appendNode('name', 'expr')
                root.appendNode('url', 'https://github.com/javecs/expr')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['ExprPublication']
    configurations = ['archives']
    pkg {
        repo = 'tools'
        name = 'expr'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/javecs/expr.git'
        version {
            name = project.version
            released  = new Date()
            gpg {
                sign = true
            }
        }
    }
}